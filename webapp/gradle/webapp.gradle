import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: "war"
apply plugin: "org.springframework.boot"
apply plugin: "application"

def MAIN_CLASS = "org.apereo.cas.web.CasWebApplication"

description = "Apereo CAS Web Application"
mainClassName = MAIN_CLASS

sourceSets {
    main {
        resources {
            srcDirs "${project.buildDir}/generated-src/main/resources", "${project.projectDir}/../resources"
        }
    }
}

javadoc {
    enabled false
}


if (rootProject.enableJRebelAgent) {
    war.dependsOn(generateRebel)
}

springBoot  {
    mainClass = MAIN_CLASS
}

bootRepackage {
    mainClass = MAIN_CLASS
    enabled = !rootProject.publishReleases
}

bootRun {

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        dependsOn pathingJar
        doFirst {
          classpath = files("$buildDir/classes/main", "$buildDir/resources/main", pathingJar.archivePath)
        }
    }

    addResources = true
    systemProperties = System.properties

    def list = []
    if (rootProject.enableRemoteDebugging) {
        list.add("-Xrunjdwp:transport=dt_socket,address=5000,server=y,suspend=n")
    }
    if (rootProject.enableJRebelAgent) {
        def lib = System.getProperty("JREBEL_LIB_PATH")
        list.add("-agentpath:$lib")
        list.add("-Drebel.remoting_plugin=true")
    }
    jvmArgs = list
}

war {
    def currentTime = java.time.ZonedDateTime.now()

    manifest {
        attributes("Implementation-Title": project.name,
                "Implementation-Vendor": project.group,
                "Created-By": project.group,
                "Implementation-Date": currentTime,
                "Specification-Version": rootProject.currentRevision,
                "Implementation-Version": project.version)
    }
    from "${project.buildDir}/generated-src/main/resources", { into "WEB-INF/classes" }
    enabled = !rootProject.publishReleases && !rootProject.publishSnapshots
}

dependencies {
    implementation project(":core:cas-server-core")
    implementation project(":core:cas-server-core-monitor")
    implementation project(":core:cas-server-core-authentication")
    implementation project(":core:cas-server-core-services")
    implementation project(":core:cas-server-core-logout")
    implementation project(":core:cas-server-core-audit")
    implementation project(":core:cas-server-core-logging")
    implementation project(":core:cas-server-core-tickets")
    implementation project(":core:cas-server-core-web")
    implementation project(":core:cas-server-core-validation")
    implementation project(":core:cas-server-core-util")
    implementation project(":core:cas-server-core-events")
    implementation project(":core:cas-server-core-events-configuration")
    implementation project(":core:cas-server-core-configuration")
    implementation project(":core:cas-server-core-configuration-metadata-repository")

    // implementation project(":support:cas-server-support-throttle")
    // implementation project(":support:cas-server-support-person-directory")
    // implementation project(":support:cas-server-support-configuration")
    // implementation project(":support:cas-server-support-geolocation")
    // implementation project(":support:cas-server-support-actions")
    // implementation project(":support:cas-server-support-cookie")
    // implementation project(":support:cas-server-support-reports")
    // implementation project(":support:cas-server-support-themes")
    // implementation project(":support:cas-server-support-validation")
    // implementation project(":support:cas-server-support-pm-webflow")
    

    implementation project(":webapp:cas-server-webapp-config")
    implementation project(":webapp:cas-server-webapp-init")

    implementation libraries.pac4j
    implementation libraries.thymeleaf
    implementation libraries.groovy
    implementation libraries.cassecurityfilter
    implementation libraries.metrics
    implementation libraries.bouncycastle
    runtime libraries.webjars
    implementation libraries.springcloudconfigclient
}
